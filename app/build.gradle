apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt' // This is needed for the dagger
apply from: 'versioning.gradle'
apply plugin: 'com.android.application' //or apply plugin: 'java'
apply plugin: 'me.tatarka.retrolambda'

// This is needed for the dagger
buildscript {
    repositories {
        jcenter()

        mavenCentral()
    }

    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
        classpath 'me.tatarka:gradle-retrolambda:3.2.4'
    }
}

// Required because retrolambda is on maven central
repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/tbruyelle/tbruyelle" }
}



/**
 * Define project specific variables
 */
project.ext.appName = "MyRules"
project.ext.versionName = "1.0"

/**
 * Generates the apk name:
 * 1. with build number
 * [variant]_[build_number]_[app_name]_[version].apk
 * 2. without build number
 * [variant]_[app_name]_[version]_[user]@[machine].apk
 */
def getAPKFileName = { variant ->

    StringBuilder sb = new StringBuilder();
    String variantName = variant.name
    sb.append(variantName)
    sb.append("_")

    if (project.hasBuildNumber) {
        // we have a build number
        sb.append("" + project.buildNumber)
        sb.append("_")
        sb.append(project.appName)
        sb.append("_")
        sb.append(versionName)
    } else {
        // we don't have a build number
        sb.append(project.appName)
        sb.append("_")
        sb.append(versionName)
        sb.append("_")
        sb.append(java.lang.System.getenv('USERNAME'))
        sb.append("@")
        sb.append(java.lang.System.getenv('COMPUTERNAME'))
    }

    sb.append(".apk")
    return sb.toString()
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        debug {
            storeFile file("cert/debug.keystore")
        }
    }


    defaultConfig {
        applicationId "com.exarlabs.android.myrules.myrules"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 2
        versionName project.versionName

        // configure some project specific fields
        buildConfigField project.INT, project.BUILD_NUMBER, ("" + project.buildNumber)
        buildConfigField project.STRING, project.BUILD_TIME, project.buildTime
        buildConfigField project.BOOLEAN, project.HAS_BUILD_NUMBER, ("" + project.hasBuildNumber)
        buildConfigField project.STRING, project.USERNAME, ("\"" + java.lang.System.getenv('USERNAME') + "\"")
        buildConfigField project.STRING, project.COMPUTERNAME, ("\"" + java.lang.System.getenv('COMPUTERNAME') + "\"")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    output.outputFile = new File(
                            output.outputFile.parent,
                            getAPKFileName(variant))
                }
            }
        }
    }

    // This is needed for the dagger
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

dependencies {

    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'

    // ------------------------------------------------------------------------
    // SUPPORT
    // ------------------------------------------------------------------------
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'

    // ------------------------------------------------------------------------
    // DATABASE
    // ------------------------------------------------------------------------
    compile 'de.greenrobot:greendao:2.0.0'

    // ------------------------------------------------------------------------
    // TOOLS
    // ------------------------------------------------------------------------
    // Dependency Injection
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.google.dagger:dagger-compiler:2.0.1'
    compile 'com.google.dagger:dagger:2.0.1'
    provided 'javax.annotation:jsr250-api:1.0'

    //Rx
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'io.reactivex:rxjava:1.0.16'
    compile 'com.jakewharton.rxbinding:rxbinding:0.3.0'
    // Rx - permissions
    compile 'com.tbruyelle.rxpermissions:rxpermissions:0.5.1@aar'

    // GSON
    compile 'com.google.code.gson:gson:2.5'

    // ------------------------------------------------------------------------
    // UI
    // ------------------------------------------------------------------------

    compile 'com.norbsoft.typefacehelper:library:0.9.0'
    compile 'com.mikepenz:octicons-typeface:3.0.0.1@aar'
    compile 'com.mikepenz:fontawesome-typeface:4.4.0.1@aar'
    compile 'com.mikepenz:typeicons-typeface:2.0.7.1@aar'
    compile 'com.mikepenz:iconics-core:2.2.0'
    compile('com.mikepenz:materialdrawer:4.3.8@aar') {
        transitive = true
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'appcompat-v7'
    }

    // floating action button
    compile 'com.github.shell-software:fab:1.1.2'

    // expandable layout
    compile 'com.github.aakira:expandable-layout:1.4.1@aar'
    compile 'com.jmedeisis:draglinearlayout:1.1.0'

    // Spinner
    compile 'com.github.ganfra:material-spinner:1.1.1'
    // ------------------------------------------------------------------------
    // Development / Analytics
    // ------------------------------------------------------------------------
    compile 'com.facebook.stetho:stetho:1.2.0'

}
